apply plugin: 'com.android.library'
apply plugin: 'bintray-release' // must be applied after your artifact generating plugin (eg. java / com.android.library)

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.2.10'
    }
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_VERSION)
        versionCode Integer.parseInt(project.LIB_VERSION_CODE)
        versionName project.LIB_VERSION_NAME
    }
}

publish {
    /**
     * Properties for BinTray upload
     */
    Properties properties = new Properties()
    File bintrayPropertiesFile = project.file('bintray.properties');
    if (bintrayPropertiesFile.exists()) {
        properties.load(bintrayPropertiesFile.newDataInputStream())
    }
    def btrayUser = properties.getProperty("bintray.user", "")
    def btrayKey = properties.getProperty("bintray.apikey", "")

    bintrayUser = btrayUser
    bintrayKey = btrayKey
    userOrg = btrayUser
    groupId = 'net.mediavrog.irr'
    artifactId = 'integrated-rating-request'
    licences = "['MIT']"
    publishVersion = libVersion
    description = 'Ask your users for app ratings the nice way.'
    website = 'https://github.com/mediavrog/integrated-rating-request'
    issueTracker = 'https://github.com/mediavrog/integrated-rating-request/issues'
    repository = 'https://github.com/mediavrog/integrated-rating-request.git'
    autoPublish = false
    dryRun = false
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
}
